/**
 * Copyright (c) KMG. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 */

import org.gradle.internal.jvm.Jvm

import java.util.regex.Matcher

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'gradle.plugin.com.palantir.graal', name:'gradle-graal' , version:graalvmPluginVersion
        classpath group: 'com.github.spotbugs.snom', name:'spotbugs-gradle-plugin', version:spotbugsPluginVersion
        classpath group: 'org.hidetake', name:'gradle-ssh-plugin', version: sshPluginVersion
        classpath group: 'ru.vyarus', name:'gradle-mkdocs-plugin', version: mkdocsPluginVersion
    }

    println "Build JVM Version    : " + Jvm.current()
    println "Build Gradle Version : $gradle.gradleVersion"
}


apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'checkstyle'

def SbkMainClass = "io.sbk.main.SbkMain"

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'checkstyle'
    apply plugin: 'application'
    apply plugin: "com.palantir.graal"
   // apply plugin: "com.github.spotbugs"

    apply from: "$rootDir/gradle/application.gradle"
    apply from: "$rootDir/gradle/checkstyle.gradle"
    apply from: "$rootDir/gradle/eclipse.gradle"
    apply from: "$rootDir/gradle/spotbugs.gradle"
    apply from: "$rootDir/gradle/idea.gradle"
    apply from: "$rootDir/gradle/jacoco.gradle"
    apply from: "$rootDir/gradle/java.gradle"
    apply from: "$rootDir/gradle/protobuf.gradle"
    apply from: "$rootDir/gradle/mkdocs.gradle"

    def sbkProjectName = rootProject.name + project.path.replace(":", "-")
    sbkProjectName = sbkProjectName.replace(rootProject.name + "-" + rootProject.name + "-", rootProject.name + "-")
    sbkProjectName = sbkProjectName.replace(rootProject.name + "-driver-", rootProject.name + "-")
    sbkProjectName = sbkProjectName.replace("--", "-")
    if (sbkProjectName == rootProject.name + "-") {
        sbkProjectName = rootProject.name
    }
    archivesBaseName = sbkProjectName
    version = sbkVersion

    def sbkClass = ""
    if (project.path.startsWith(":driver-")) {
        sbkClass = project.path.replace(":driver-", "")
    }
    jar {
        manifest {
            attributes 'Name': sbkProjectName,
                    'Specification-Title': "Storage Benchmark Kit",
                    'Specification-Version': sbkVersion,
                    'Specification-Vendor': "KMG",
                    'Implementation-Title': sbkProjectName,
                    'Implementation-Version': sbkVersion,
                    'Implementation-Vendor': "KMG"
        }
    }

    distributions {
        main {
            distributionBaseName.set(archivesBaseName)
        }
    }

    application {
        mainClass = SbkMainClass
        applicationName = archivesBaseName
        applicationDefaultJvmArgs = ['-Dsbk.applicationName=' + applicationName, '-Dsbk.className=' + sbkClass,
                                     '-Dsbk.appHome=SBK_APP_HOME']
    }

    task generateDashboards(type: Copy) {
        if (project.path.startsWith(":driver-")) {
            def outDir = 'build/dashboard'
            def outFile = sbkProjectName + "-" + sbkVersion + ".json"
            def sbkClassUpperCase = sbkClass.toUpperCase()

            /*
            call with parameter
            ./gradlew generateDashboards -Poutdir="/Users/kmg/projects/SBK/grafana/dashboards"
            to generate dashboard files on single folder
             */
            if (project.hasProperty("outdir")) {
                outDir = outdir
                outFile = sbkProjectName + ".json"
            }


            from "$rootDir/dashboard-template.json"
            into outDir
            filter { line -> line.replaceAll('SBK@2020', sbkClassUpperCase) }
            rename 'dashboard-template.json', outFile

            doLast {
                println "generatering file : " + outDir + "/" + outFile
            }
        }
    }

    task generateDockerfiles() {
        def baseDriverToSkip = "sbk-api"
        def buffer = ""
        def outDir = 'build/dockers'
        def outFile = sbkProjectName + "-" + sbkVersion

        /*
                call with parameter
            ./gradlew generateDockerfiles -Poutdir="/Users/kmg/projects/SBK/dockers"
            to generate dashboard files on single folder
        */
        if (project.hasProperty("outdir")) {
            outDir = outdir
            outFile = sbkProjectName
        }


        if (project.path.startsWith(":driver-")) {
            doLast {
                println "generatering file : " + outDir + "/" + outFile

                configurations.runtime.resolvedConfiguration.resolvedArtifacts.each { artifact ->
                    def id = artifact.moduleVersion.id
                    if (id.group.toString() == rootProject.name) {
                        if (id.name != baseDriverToSkip) {
                            buffer += "COPY --chown=root:root " + id.name + " \${APP_HOME}/" + id.name + "\n"
                        }
                    }
                }
                buffer += "COPY --chown=root:root " + project.name + " \${APP_HOME}/" + project.name + "\n"
                copy {
                    from "$rootDir/driver-docker-template"
                    into outDir
                    filter { line -> line.replaceAll('PROJECT@SBK', project.name) }
                    filter { line -> line.replaceAll('APP@SBK', sbkProjectName) }
                    filter { line -> line.replace("COPY@SBK", buffer) }
                    rename 'driver-docker-template', outFile
                }
            }
        } else if (project.name == rootProject.name) {
            doLast {
                println "generatering file : " + outDir + "/" + outFile

                configurations.runtime.resolvedConfiguration.resolvedArtifacts.each { artifact ->
                    def id = artifact.moduleVersion.id
                    if (id.group.toString() == rootProject.name) {
                        if (id.name != baseDriverToSkip) {
                            buffer += "COPY --chown=root:root " + id.name + " \${APP_HOME}/" + id.name + "\n"
                        }
                    }
                }
                copy {
                    from "$rootDir/docker-template"
                    into outDir
                    filter { line -> line.replace("COPY@SBK", buffer) }
                    rename 'docker-template', outFile
                }
            }
        }
    }

    graal {
        def className = sbkClass.capitalize();
        mainClass SbkMainClass
        outputName sbkProjectName
        javaVersion '11'
        graalVersion '21.0.0'
        option "-Dsbk.applicationName=$applicationName -Dsbk.className=$sbkClass"
        option "--initialize-at-build-time=io.sbk.$className"
        option "-H:+AllowIncompleteClasspath"
        option "-H:ResourceConfigurationFiles=$rootDir/resource-config.json"
        option "-H:ReflectionConfigurationFiles=$rootDir/reflection-config.json"
        option "--no-fallback"
        option "-H:Optimize=3"
    }

}

task printDependencies {
    doLast {
        project.subprojects.findAll().each {
            println "subproject : " + it.name
            it.configurations.runtime.resolvedConfiguration.resolvedArtifacts.each { artifact ->
                def id = artifact.moduleVersion.id
                if (id.group.toString() == rootProject.name) {
                    println "\tgroup: ${id.group}, version: ${id.version},  name: ${id.name}"
                }
            }
            println()
        }
        println("Root project : " + rootProject.name)
        configurations.runtime.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            def id = artifact.moduleVersion.id
            if (id.group.toString() == rootProject.name) {
                println "\tgroup: ${id.group}, version: ${id.version},  name: ${id.name}"
            }
        }
        println()
    }
}


/*
   Example command : ./gradlew printdrivers -Preadme
   another command : ./gradlew printdrivers -Preadme -Pdec   (add the project description)
 */
task printDrivers {
    String readmePrefix = "https://github.com/kmgowda/SBK/tree/master/"

    doLast {
        int i = 1;
        println();
        project.subprojects.findAll().each {
            if (it.name.startsWith("driver-") && !it.name.equals("driver-template")) {
                String out = it.name
                String dec = it.getDescription()
                if (project.hasProperty("readme")) {
                    String name = it.name.replace("driver-", "")
                    if (dec != null && project.hasProperty("dec")) {
                        out = "[" + name.capitalize() + "](" + readmePrefix + it.name + " \"" + dec + "\"" + ")"
                    } else {
                        out = "[" + name.capitalize() + "](" + readmePrefix + it.name + ")"
                    }
                }
                printf("%4d. %s\n", i, out)
                i++;
            }
        }
    }
}


/* Include your driver below */
dependencies {
    api project(":sbk-gem")
    api project(":sbk-ram")
    api project(":sbk-gem")
    api project(":sbk-gem-yal")
    api project(":driver-concurrentq")
    api project(":driver-file")
    api project(":driver-filestream")
    api project(":driver-asyncfile")
    api project(":driver-hdfs")
    api project(":driver-bookkeeper")
    api project(":driver-rabbitmq")
    api project(":driver-rocketmq")
    api project(":driver-pulsar")
    api project(":driver-kafka")
    api project(":driver-pravega")
    api project(":driver-nats")
    api project(':driver-natsStream')
    api project(':driver-artemis')
    api project(':driver-nsq')
    api project(':driver-jdbc')
    api project(':driver-minio')
    api project(':driver-foundationdb')
    api project(':driver-fdbrecord')
    api project(':driver-mongodb')
    api project(':driver-rocksdb')
    api project(':driver-ignite')
    api project(':driver-couchdb')
    api project(':driver-hive')
    api project(':driver-null')
    api project(':driver-sqlite')
    api project(':driver-mysql')
    api project(':driver-mariadb')
    api project(':driver-postgresql')
    api project(':driver-derby')
    api project(':driver-mssql')
    api project(':driver-redpanda')
    api project(':driver-csv')
    api project(':driver-redis')
    api project(':driver-seaweeds3')
    api project(':driver-cassandra')
    api project(':driver-db2')
    api project(':driver-cephs3')
    api project(':driver-openio')
    api project(':driver-leveldb')
}

//create a single Jar with all dependencies
task buildJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': rootProject.name,
                'Implementation-Version': sbkVersion,
                'Main-Class': SbkMainClass
    }
    zip64 true
    archiveBaseName = rootProject.name
    from { configurations.api.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    with jar
}

task SbkGem(type: CreateStartScripts) {
    mainClass = 'io.sbk.main.SbkGemMain'
    applicationName = 'sbk-gem'
    defaultJvmOpts = ['-Dsbk.applicationName=' + rootProject.name, '-Dsbk.appHome=SBK_APP_HOME']
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath

    SbkGem {
        doLast {
            unixScript.text = unixScript.text.replace('SBK_APP_HOME', '\$APP_HOME')
            windowsScript.text = windowsScript.text.replace('SBK_APP_HOME', '%~dp0..')
            // Replace the classpath on windows with the pathing JAR and the main archive
            windowsScript.text = windowsScript.text.replaceAll('set CLASSPATH=.*', Matcher.quoteReplacement(
                    "set CLASSPATH=%APP_HOME%\\lib\\${pathingJar.archiveFileName};%APP_HOME%\\lib\\${jar.archiveFileName}"))
        }
    }
}

task SbkGemYal(type: CreateStartScripts) {
    mainClass = 'io.sbk.main.SbkGemYalMain'
    applicationName = 'sbk-gem-yal'
    defaultJvmOpts = ['-Dsbk.applicationName=' + rootProject.name, '-Dsbk.appHome=SBK_APP_HOME']
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath

    SbkGemYal {
        doLast {
            unixScript.text = unixScript.text.replace('SBK_APP_HOME', '\$APP_HOME')
            windowsScript.text = windowsScript.text.replace('SBK_APP_HOME', '%~dp0..')
            // Replace the classpath on windows with the pathing JAR and the main archive
            windowsScript.text = windowsScript.text.replaceAll('set CLASSPATH=.*', Matcher.quoteReplacement(
                    "set CLASSPATH=%APP_HOME%\\lib\\${pathingJar.archiveFileName};%APP_HOME%\\lib\\${jar.archiveFileName}"))
        }
    }
}

applicationDistribution.into("bin") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(SbkGem)
    fileMode = 0755
}

applicationDistribution.into("bin") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(SbkGemYal)
    fileMode = 0755
}